// SPDX-License-Identifier: GPL-3.0
// solhint-disable const-name-snakecase
pragma solidity ^0.8.20;

import {CipherVKeyConst} from "./CipherVKeyConst.sol";
import {Proof} from "./CipherStorage.sol";

contract CipherVerifier is CipherVKeyConst {
    // Memory data
    uint16 internal constant pVk = 0;
    uint16 internal constant pPairing = 128;
    uint16 internal constant pLastMem = 896;

    function verifyProof(Proof calldata proof, bytes2 _type) public view returns (bool) {
        // solhint-disable-next-line no-inline-assembly
        assembly {
            function checkField(v) {
                if iszero(lt(v, q)) {
                    mstore(0, 0)
                    return(0, 0x20)
                }
            }

            /// @notice Elliptic curve multiplication then accumulation
            /// @dev pR = pR + (x, y) * s
            /// @param pR The pointer to the result
            /// @param x The x coordinate of the point
            /// @param y The y coordinate of the point
            /// @param s The scalar
            function eccMulAcc(pR, x, y, s) {
                let success
                let mIn := mload(0x40)
                mstore(mIn, x)
                mstore(add(mIn, 32), y)
                mstore(add(mIn, 64), s)

                success := staticcall(sub(gas(), 2000), 7, mIn, 96, mIn, 64)

                if iszero(success) {
                    mstore(0, 0)
                    return(0, 0x20)
                }

                mstore(add(mIn, 64), mload(pR))
                mstore(add(mIn, 96), mload(add(pR, 32)))

                success := staticcall(sub(gas(), 2000), 6, mIn, 128, pR, 64)

                if iszero(success) {
                    mstore(0, 0)
                    return(0, 0x20)
                }
            }

            /// @notice Get the fixed size configurations for the given utxo type
            /// @param utxoType The utxo type
            /// @return IC0x The x coordinate of the default point
            /// @return IC0y The y coordinate of the default point
            /// @return IC1x The x coordinate of the input a[0]
            /// @return IC1y The y coordinate of the input a[1]
            /// @return IC2x The x coordinate of the first b[0][0]
            /// @return IC2y The y coordinate of the first b[0][1]
            /// @return IC3x The x coordinate of the first b[1][0]
            /// @return IC3y The y coordinate of the first b[1][1]
            /// @return IC4x The x coordinate of the output c[0]
            /// @return IC4y The y coordinate of the output c[1]
            function getFixedSizeConfigs(utxoType) -> IC0x, IC0y, IC1x, IC1y, IC2x, IC2y, IC3x, IC3y, IC4x, IC4y {
                switch utxoType
<% for (let i = 0; i < IcCases.length; i++) __append(IcCases[i]) %>
                default {
                    // this is not allowed
                    mstore(0, 0)
                    return(0, 0x20)
                }
            }

            function getDeltas(utxoType) -> deltax1, deltax2, deltay1, deltay2 {
                switch utxoType
<% for (let i = 0; i < DeltaCases.length; i++) __append(DeltaCases[i]) %>
                default {
                    // this is not allowed
                    mstore(0, 0)
                    return(0, 0x20)
                }
            }

            function dynamicMulAcc(_pVk, inputNullifiersOffset, outputCommitmentsOffset, utxoType) {
                switch utxoType
<% for (let i = 0; i < MulAccCases.length; i++) __append(MulAccCases[i]) %>
                default {
                    // this is not allowed
                    mstore(0, 0)
                    return(0, 0x20)
                }
            }

            /**
             * | calldata layout:
             * | ------------------ | ------------- | ----------------- | ----------------------------------------------------
             * | pos                | pos           | pos               | value
             * | (absolute)         | (rel proof)   | (rel pubSignals)  |
             * | 0                  |               |                   | function selector
             * | 4                  |               |                   | proof.offset (64)
             * | 36                 |               |                   | uxtoType (right-padded to 32 bytes)
             * | ------------------ | ------------- | ----------------- | ----------------------------------------------------
             * | 68 + 0             | 0             |                   | proof.a[0]
             * | 68 + 32            | 32            |                   | proof.a[1]
             * | 68 + 64            | 64            |                   | proof.b[0][0]
             * | 68 + 96            | 96            |                   | proof.b[0][1]
             * | 68 + 128           | 128           |                   | proof.b[1][0]
             * | 68 + 160           | 160           |                   | proof.b[1][1]
             * | 68 + 192           | 192           |                   | proof.c[0]
             * | 68 + 224           | 224           |                   | proof.c[1]
             * | 68 + 256           | 256           |                   | proof.publicSignals.offset (288)
             * | ------------------ | ------------- | ----------------- | ----------------------------------------------------
             * | 68 + 288           | 288           | 0                 | proof.publicSignals.root
             * | 68 + 320           | 320           | 32                | proof.publicSignals.publicInAmt
             * | 68 + 352           | 352           | 64                | proof.publicSignals.publicOutAmt
             * | 68 + 384           | 384           | 96                | proof.publicSignals.publicInfoHash
             * | 68 + 416           | 416           | 128               | proof.publicSignals.inputNullifiers.offset (192)
             * | 68 + 448           | 448           | 160               | proof.publicSignals.outputCommitments.offset (224 + 32*n)
             * | 68 + 480           | 480           | 192               | proof.publicSignals.inputNullifiers.length (n)
             * | 68 + 512           | 512           | 224               | proof.publicSignals.inputNullifiers[0]
             * | 68 + ...           | ...           | ...               | ...
             * | 68 + 512 + 32*n    | 512 + 32*n    | 224 + 32n         | proof.publicSignals.outputCommitments.length (m)
             * | 68 + 544 + 32*n    | 544 + 32*n    | 256 + 32n         | proof.publicSignals.outputCommitments[0]
             * | 68 + ...           | ...           | ...               | ...
             * | ------------------ | ------------- | ----------------- | ----------------------------------------------------
             */

            let pMem := mload(0x40)
            mstore(0x40, add(pMem, pLastMem))

            // Validate that all evaluations âˆˆ F
            // check field for root, publicInAmt, publicOutAmt, publicInfoHash
            checkField(calldataload(add(proof, 288))) // proof.publicSignals.root
            checkField(calldataload(add(proof, 320))) // proof.publicSignals.publicInAmt
            checkField(calldataload(add(proof, 352))) // proof.publicSignals.publicOutAmt
            checkField(calldataload(add(proof, 384))) // proof.publicSignals.publicInfoHash

            // check field for inputNullifiers
            let pos := add(proof, 480)
            let bytesLen := mul(calldataload(pos), 32) // inputNullifiers.length * 32
            pos := add(pos, 32) // inputNullifiers[0], if length > 0
            let end := add(bytesLen, pos)
            for {

            } lt(pos, end) {
                pos := add(pos, 32)
            } {
                checkField(calldataload(pos))
            }

            // check field for outputCommitments
            bytesLen := mul(calldataload(pos), 32) // outputCommitments.length * 32
            pos := add(pos, 32) // outputCommitments[0], if length > 0
            end := add(bytesLen, pos)
            for {

            } lt(pos, end) {
                pos := add(pos, 32)
            } {
                checkField(calldataload(pos))
            }

            let _pPairing := add(pMem, pPairing)
            let _pVk := add(pMem, pVk)

            let IC0x, IC0y, IC1x, IC1y, IC2x, IC2y, IC3x, IC3y, IC4x, IC4y := getFixedSizeConfigs(_type)

            mstore(_pVk, IC0x)
            mstore(add(_pVk, 32), IC0y)

            let publicSignalsOffset := add(proof, 256)
            eccMulAcc(_pVk, IC1x, IC1y, calldataload(add(publicSignalsOffset, 32)))
            eccMulAcc(_pVk, IC2x, IC2y, calldataload(add(publicSignalsOffset, 64)))
            eccMulAcc(_pVk, IC3x, IC3y, calldataload(add(publicSignalsOffset, 96)))
            eccMulAcc(_pVk, IC4x, IC4y, calldataload(add(publicSignalsOffset, 128)))

            let inputNullifiersOffset := add(add(publicSignalsOffset, 32), calldataload(add(proof, 416)))
            let outputCommitmentsOffset := add(add(publicSignalsOffset, 32), calldataload(add(proof, 448)))
            dynamicMulAcc(_pVk, inputNullifiersOffset, outputCommitmentsOffset, _type)

            // -A
            mstore(_pPairing, calldataload(proof))
            mstore(add(_pPairing, 32), mod(sub(q, mod(calldataload(add(proof, 32)), q)), q))

            // B
            let pB := add(proof, 64)
            mstore(add(_pPairing, 64), calldataload(pB))
            mstore(add(_pPairing, 96), calldataload(add(pB, 32)))
            mstore(add(_pPairing, 128), calldataload(add(pB, 64)))
            mstore(add(_pPairing, 160), calldataload(add(pB, 96)))

            // alpha1
            mstore(add(_pPairing, 192), alphax)
            mstore(add(_pPairing, 224), alphay)

            // beta2
            mstore(add(_pPairing, 256), betax1)
            mstore(add(_pPairing, 288), betax2)
            mstore(add(_pPairing, 320), betay1)
            mstore(add(_pPairing, 352), betay2)

            // vk_x
            mstore(add(_pPairing, 384), mload(_pVk))
            mstore(add(_pPairing, 416), mload(add(_pVk, 32)))

            // gamma2
            mstore(add(_pPairing, 448), gammax1)
            mstore(add(_pPairing, 480), gammax2)
            mstore(add(_pPairing, 512), gammay1)
            mstore(add(_pPairing, 544), gammay2)

            // C
            let pC := add(proof, 192)
            mstore(add(_pPairing, 576), calldataload(pC))
            mstore(add(_pPairing, 608), calldataload(add(pC, 32)))

            // delta2
            let deltax1, deltax2, deltay1, deltay2 := getDeltas(_type)
            mstore(add(_pPairing, 640), deltax1)
            mstore(add(_pPairing, 672), deltax2)
            mstore(add(_pPairing, 704), deltay1)
            mstore(add(_pPairing, 736), deltay2)

            let success := staticcall(sub(gas(), 2000), 8, _pPairing, 768, _pPairing, 0x20)
            success := and(success, mload(_pPairing))

            mstore(0, success)
            return(0, 0x20)
        }
    }
}
